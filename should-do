#!/usr/bin/perl
use strict;
use warnings;

my %activity_weight = (
    read_blog    => 1,     # 1 pomodoro, i.e. 25 mins
    wrote_blog   => 2,     # 50 mins
    read_chapter => 2,
    wrote_script => 4,
    read_book    => 30,    # 12.5 hour
    wrote_prog   => 30,
);

my $activities_file = shift;
die "Usage: $0 activities.txt\n" unless defined $activities_file;

open my $FH, $activities_file or die "Can't open $activities_file: $!\n";

my $activities;
while (<$FH>) {
    next if /^\s*#/;
    chomp;
    my ( $skills, $activity, $url, $date ) = split /\|/;
    die
      "Unknown activity '$activity' at line $. of '$activities_file' file. Exiting ...\n"
      unless grep $activity eq $_, keys %activity_weight;
    for my $skill ( split /,/, $skills ) {
        push @{ $activities->{$skill} },
          { activity => $activity, url => $url, date => $date };
    }
}

close $FH;

# calculate activities per skill
my %activities;
for my $skill ( keys %{$activities} ) {
    for my $activity ( @{ $activities->{$skill} } ) {
        $activities{$skill} += $activity_weight{ $activity->{activity} };
    }
}

my $header = <<END;
You earn your living by having enough knowledge (theory) and experience
(practice). However, these are expiring assets so you have to keep them fresh.
While honing your knowledge keep in mind these principles:

* Invest regularly - the *habit* itself is more important than the amount
* Diversify - know the ins and outs of the technology you are working with
  currently but *don't stop* there
* Review and rebalance

The skills you should be working on:

END

# print out in Markdown format
print $header;
for my $skill ( sort { $activities{$a} <=> $activities{$b} } keys %activities )
{
    print "* $skill ($activities{$skill}): ";
    my @activities;
    for ( @{ $activities->{$skill} } ) {
        my $url = $_->{url} =~ /^http/
          ? "[$_->{activity}]($_->{url})"   # real URL
          : "$_->{activity} ($_->{url})";   # note, script name, book title, ...
        push @activities, "$_->{date} - $url";
    }
    print join ", ", @activities;
    print "\n";
}
