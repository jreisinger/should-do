#!/usr/bin/perl
use strict;
use warnings;

my @skills = qw(
  OS
  Prog
  Net
  Sec
  Mngt
);

my %activity_weight = (
    wrote_blog   => 2,     # 2 pomodoros, i.e. 50 mins
    wrote_script => 4,
    read_book    => 30,    # 30 pomodoros, i.e. 12.5 hour
    wrote_prog   => 30,
);

my $activities_file = shift;
die "Usage: $0 activities.txt\n" unless defined $activities_file;

open my $FH, $activities_file or die "Can't open $activities_file: $!\n";

my $activities;
while (<$FH>) {
    next if /^\s*#/;
    chomp;
    my ( $skills, $activity, $url, $date ) = split /\|/;
    die
      "Unknown activity '$activity' at line $. of '$activities_file' file. Exiting ...\n"
      unless grep $activity eq $_, keys %activity_weight;
    for my $skill ( split /,/, $skills ) {
        die "You don't need $skill skill. Exiting ...\n"
          unless grep $skill eq $_, @skills;
        push @{ $activities->{$skill} },
          { activity => $activity, url => $url, date => $date };
    }
}

close $FH;

# calculate activities per skill
my %activities;
for my $skill (@skills) {
    $activities{$skill} = 0 if not exists $activities->{$skill};
    for my $activity ( @{ $activities->{$skill} } ) {
        $activities{$skill} += $activity_weight{ $activity->{activity} };
    }
}

my $header = <<END;
You earn your living by having enough knowledge (theory) and experience
(practice). However, these are expiring assets so you have to keep them fresh.
While honing your knowledge keep in mind these principles:

* Invest regularly - the *habit* itself is more important than the amount
* Diversify - know the ins and outs of the technology you are working with
  currently but *don't stop* there
* Review and rebalance

The skills you should be working on:

END

# print out in Markdown format
print $header;
for my $skill ( sort { $activities{$a} <=> $activities{$b} } keys %activities )
{
    print "* $skill ($activities{$skill}): ";
    my @activities;
    for ( @{ $activities->{$skill} } ) {
        my $url = $_->{url} =~ /^http/
          ? "[$_->{activity}]($_->{url})"   # real URL
          : "$_->{activity} ($_->{url})";   # note, script name, book title, ...
        push @activities, "$_->{date} - $url";
    }
    print join ", ", @activities;
    print "\n";
}
